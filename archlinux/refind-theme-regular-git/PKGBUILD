# Maintainer: Jeremy Asuncion <jeremyasuncion808@gmail.com>
  
pkgname='refind-theme-regular-git'
pkgver=r42.7a282d0
pkgrel=1
pkgdesc="A simplistic clean and minimal theme for rEFInd"
arch=('any')
url="https://github.com/bobafetthotmail/refind-theme-regular"
license=('AGPL3' 'custom:OFL' 'custom:Ubuntu Font License 1.0')
depends=('refind-efi')
provides=('refind-theme-regular')
conflicts=('refind-theme-regular')
makedepends=('git')
source=('git+https://github.com/bobafetthotmail/refind-theme-regular')
md5sums=('SKIP')

pkgver() {
    cd "${pkgname%-git}"
    printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
    # Specify path to the refind directory
    _refind_home="$pkgdir/boot/EFI/rEFInd"

    # Fix theme info
    _config="$srcdir/${pkgname%-git}/theme.conf"
    sed -i 's|128|placeholder|g'                            "${_config}"
    sed -i 's|384|128|g'                                    "${_config}"
    sed -i 's|placeholder|384|g'                            "${_config}"
    sed -i 's|48|placeholder|g'                             "${_config}"
    sed -i 's|144|48|g'                                     "${_config}"
    sed -i 's|placeholder|144|g'                            "${_config}"
    sed -i 's|\(fonts/[a-z,A-Z,-]*\)[0-9]*\.png|\118.png|g' "${_config}"
    sed -i 's|bg\.png|bg_dark.png|g' "${_config}"

    # Install to directory
    install -D "$srcdir/${pkgname%-git}/theme.conf" "$_refind_home/${pkgname%-git}/theme.conf"
    cp -r "$srcdir/${pkgname%-git}/fonts" "$_refind_home/${pkgname%-git}"
    cp -r "$srcdir/${pkgname%-git}/icons" "$_refind_home/${pkgname%-git}"
}
