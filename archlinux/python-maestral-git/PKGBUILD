# Maintainer: Batuhan Baserdem <lastname dot firstname at gmail>

pkgname=python-maestral-git
pkgdesc='A light-weight and open-source Dropbox client.'
arch=('any')
pkgver=0.2.3.27.g7717c64
pkgrel=5
url="https://pypi.org/project/maestral/"
license=('MIT')
depends=(
    'python'
    'python-click'
    'python-dropbox'
    'python-watchdog'
    'python-blinker'
    'python-requests'
    'python-u-msgpack'
    'python-keyrings-alt'
    'python-keyring'
    'python-six'
    'python-chardet'
    'python-idna'
    'python-urllib3'
    'python-entrypoints'
    'python-secretstorage'
    'python-yaml'
    'python-argh'
    'python-pathtools'
    'python-cryptography'
    'python-jeepney'
    'python-cffi'
    'python-asn1crypto'
    'python-pycparser'
    'python-setuptools'
    'python-pyqt5'
    'pyqt5-common')
makedepends=('python-setuptools' 'git')
source=("git+https://github.com/SamSchott/maestral-dropbox.git")
md5sums=('SKIP')

pkgver() {
    cd "${srcdir}/maestral-dropbox"
    git describe --long --tags | sed 's|^v\(.*\)$|\1|g' | tr - .
}

build() {
    cd "${srcdir}/maestral-dropbox"
    python setup.py build
}

package() {
    cd "${srcdir}/maestral-dropbox"
    python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
