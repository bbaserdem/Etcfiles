pkgname=maestral
_srcname="$pkgname-dropbox"
pkgver=0.2.3
pkgrel=1
pkgdesc="A light-weight and open-source Dropbox client for macOS and Linux"
arch=('any')
url="https://github.com/SamSchott/$_srcname"
license=('MIT')
depends=('python-blinker' 'python-click' 'python-dropbox' 'python-keyrings-alt' 'python-u-msgpack' 'python-watchdog')
makedepends=('gendesk' 'python-setuptools')
optdepends=('python-pyqt5: graphical user interface support')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('3437096ce76abb74f25f00d8885d8e0df300ea40002d4456b004d9353d004133')

prepare() {
  gendesk -q -n \
    --pkgname="$pkgname" \
    --exec="$pkgname-gui" \
    --comment="$pkgdesc" \
    --categories=Network
}

build() {
  cd "$_srcname-$pkgver"
  python setup.py build
}

package() {
  cd "$_srcname-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -Dm644 "$srcdir/$pkgname.desktop" -t "$pkgdir/usr/share/applications"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 "$pkgname/gui/resources/app_icon.svg" "$pkgdir/usr/share/pixmaps/$pkgname.svg"
}
